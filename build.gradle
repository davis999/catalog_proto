apply plugin: 'java'
apply plugin: 'com.google.protobuf'

// Setting the common Java and gradle wrapper, this should be the first `apply from` file.
repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// Specify the gradle wrapper version
task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // protobuf compile plugin
        classpath('com.google.protobuf:protobuf-gradle-plugin:0.8.0')
    }
}

//appoint proto file.
sourceSets {
    main {
        proto {
            srcDir "$projectDir/proto"
        }
        java {
            srcDir "src/main/protoGen"
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.0.0" }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.0.0" }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins { java { outputSubDir = 'protoGen' } }
            task.plugins { grpc { outputSubDir = 'protoGen' } }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen {
    doFirst { delete("$projectDir/src/main/protoGen") }
}

clean.dependsOn cleanProtoGen

dependencies{
    compile('io.grpc:grpc-stub:1.0.0')
    compile('io.grpc:grpc-protobuf:1.0.0')
    compile('io.grpc:grpc-netty:1.0.0')
}
